plugins {
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

ext {
	set('springCloudVersion', "2021.0.1")
	set("springBootVersion", "2.6.7")
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	group = groupPath as Object
	version = projectVersion as Object
	sourceCompatibility = '17'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
		implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.projectreactor:reactor-test'
	}

	repositories {
		mavenCentral()
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	test {
		setJvmArgs([
				'-Dspring.profiles.active=test'
		])
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.enabled(true)
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
			mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
		}
	}
}

jar {
	enabled = false
}

project(':config') {
	jar {
		archivesBaseName = "${rootProject.name}"
		enabled = false
	}

	dependencies {
		implementation project(':api:credit-card-api')
		implementation project(':integration:credit-card-integration')
		runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	}
}

project(':api') {
	jar {
		enabled = false
	}
}

project(':api:credit-card-api') {
	dependencies {
		implementation project(':integration:credit-card-integration')
	}
}

project(':integration') {
	jar {
		enabled = false
	}
}
